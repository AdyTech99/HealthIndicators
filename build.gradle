plugins {
    id 'dev.architectury.loom' version '1.10-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
    id 'io.github.pacifistmc.forgix' version "1.2.9"
}

architectury {
    minecraft = project.minecraft_version
}

forgix {
    group = rootProject.maven_group
    mergedJarName = "HealthIndicators-${rootProject.mod_version}.jar"

    neoforge {
        projectName = "neoforge"
        jarLocation = "build/libs/HealthIndicators-neoforge-${rootProject.mod_version}.jar"
//        mixin "neoforge/src/main/resources/jahealthindicators.mixins.json"
    }
    fabric {
        projectName = "fabric"
        jarLocation = "build/libs/HealthIndicators-fabric-${rootProject.mod_version}.jar"
//        mixin "fabric/src/main/resources/healthindicators.mixins.json"
    }
}

allprojects {
    group = rootProject.maven_group
    version = rootProject.mod_version
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'maven-publish'

    base {
        // Set up a suffixed format for the mod jar names, e.g. `example-fabric`.
        archivesName = "$rootProject.archives_name-$project.name"
    }

    repositories {
        maven{
            name 'Xander Maven'
            url 'https://maven.isxander.dev/releases'
        }
        maven { url "https://maven.terraformersmc.com/releases/"}
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://repo.spongepowered.org/repository/maven-public/' }
    }

    dependencies {
        minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"
        mappings loom.layered {
            it.mappings("net.fabricmc:yarn:$rootProject.yarn_mappings:v2")
            it.mappings("dev.architectury:yarn-mappings-patch-neoforge:$rootProject.yarn_mappings_patch_neoforge_version")
        }
        annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    }

    java {
        // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
        // if it is present.
        // If you remove this line, sources will not be generated.
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
    }

    // Configure Maven publishing.
    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = base.archivesName.get()
                from components.java
            }
        }

        // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
        repositories {
            // Add repositories to publish to here.
            // Notice: This block does NOT have the same function as the block in the top level.
            // The repositories here will be used for publishing your artifact, not for
            // retrieving dependencies.
        }
    }
    build.finalizedBy(mergeJars)
    assemble.finalizedBy(mergeJars)
}